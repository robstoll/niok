buildscript {
    project.group = 'ch.tutteli.niok'
    project.version ='0.2.0'
    project.description = "API for java.nio.file in a Kotlin idiomatic way"

    ext {
        // project setup
        tutteli_gradle_version = '0.14.1'
        github_user = 'robstoll'

        // main
        kotlin_version = '1.2.71'

        // test
        junit_platform_version = '1.2.0'
        atrium_version = '0.7.0'
        mockito_kotlin_version = '1.5.0'
        tutteli_spek_extensions = '0.3.0'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
        classpath "ch.tutteli:tutteli-gradle-dokka:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-publish:$tutteli_gradle_version"
        classpath "ch.tutteli:tutteli-gradle-spek:$tutteli_gradle_version"
    }
}

repositories {
    maven { url "http://dl.bintray.com/robstoll/tutteli-jars" }
}

apply plugin: 'kotlin'

apply plugin: 'ch.tutteli.dokka'
tutteliDokka.githubUser = github_user

apply plugin: 'ch.tutteli.spek'

apply plugin: 'ch.tutteli.kotlin.utils'
kotlinutils.kotlinVersion = kotlin_version

apply plugin: 'ch.tutteli.publish'
publish {
    githubUser = github_user
    bintrayRepo = 'tutteli-jars'
}

dependencies {
    compile kotlinStdlib()
    compile kotlinReflect(), withoutKotlin
    testCompile("ch.tutteli.atrium:atrium-cc-en_GB-robstoll:$atrium_version") {
        exclude group: 'org.junit.platform'
        exclude module: 'atrium-verbs'
    }
    testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version", withoutKotlin
    testCompile "ch.tutteli:tutteli-spek-extensions:$tutteli_spek_extensions"
}
/*

Release & deploy a commit
--------------------------------
1. search for X.Y.Z-SNAPSHOT and replace with X.Y.Z
2. update master:
    a) point to the tag
        1) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        2) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) commit (modified .travis.yml, build.gradle and README.md)
    c) git tag vX.Y.Z
    d) git push origin vX.Y.Z
4. deploy to bintray:
    a) gr publishToBintray
    b) Log in to bintray, check and publish new jars
5. create release on github

Prepare next dev cycle
-----------------------
1. change version in build.gradle to X.Y.Z-SNAPSHOT
2. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
3. commit & push changes

*/
